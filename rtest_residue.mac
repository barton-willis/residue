(reset(),kill(all),display2d : true, load("residue.lisp"), 0);
0$

(nounform_return(e) := if mapatom(e) then false else cons(verbify(inpart(e,0)), args(e)) ,0);   

0$

/* residue of constants */
residue(42,x,%pi);
0$

residue(42*a,x,%pi);
0$

residue(a/(b+c),x, inf);
0$

residue(%e^%pi,z, 107);
0$

residue(1/z,x,0);
0$

residue(0,z,0);
0$

/* simple powers */
map(lambda([q], residue(q,x,0)), [1/x^2,1/x,1,x,x^2]);
[0,1,0,0,0]$

map(lambda([q], residue(q,x,1)), [1/x^2,1/x,1,x,x^2]);
[0,0,0,0,0]$

map(lambda([q], residue(q,x,0)), [a/x^2,a/x,a,a*x,a*x^2]);
[0,a,0,0,0]$

 residue(rat(1/x),x,0);
 1$

residue(rat((a+b)/x),x,0);
 a+b$

/* rational functions */
residue(a/x^3+b/x^2+c/x + d + e*x,x,0);
c$

residue(a/x^3+b/x^2+c/x + d + e*x,x,%pi);
0$

residue(a/x^3+b/x^2+c/x + d + e*x,x,inf);
-c$

residue(z^3/((z-1)^4*(z-2)*(z-3)),z,1);
101/16$

residue(z^3/((z-1)^4*(z-2)*(z-3)),z,2);
-8$

residue(z^3/((z-1)^4*(z-2)*(z-3)),z,3);
27/16$

residue(1/(x - 1), x, 1);
1$

residue(1/(x - 2)^2, x, 2);
0$

residue((x + 1)/(x - 1), x, 1);
2$

residue((x^2 + 1)/(x - 3), x, 3);
10$

residue((x - 1)/(x^2 - 4), x, 2);
1/4$

residue(x/(x^2 - 1), x, 1);
1/2$

residue(x/(x^2 - 1), x, -1);
1/2$

residue(1/(x^2 + 1), x, %i);
-%i/2$

residue(1/(x^2 + 1), x, -%i);
%i/2$

residue((x + 2)/(x^2 - 4), x, 2);
1$

residue((x + 2)/(x^2 - 4), x, -2);
0$

residue(1/(x^2 - 4), x, 2);
1/4$

residue(1/(x^2 - 4), x, -2);
-1/4$

/* of course, when a = 0, this is wrong */
residue(1/(x^2 - a^2), x, a);
1/(2*a)$

residue(1/(x^2 - a^2), x, -a);
-1/(2*a)$

residue(x/(x - a)^2, x, a);
1$

residue((x - b)/(x - a)^2, x, a);
1$

residue((x^2 + 2*x + 1)/(x + 1)^2, x, -1);
0$

residue(1/((x - 1)*(x - 2)), x, 1);
-1$

residue(1/((x - 1)*(x - 2)), x, 2);
1$

/* algebraic expressions */
residue(sqrt(x)/(x - 1), x, 1);
1$

nounform_return(residue(1/sqrt(x), x, 0));
['residue,1/sqrt(x),x,0]$

nounform_return(residue(1/(x^(3/2)), x, 0));
['residue,1/(x^(3/2)), x, 0]$

nounform_return(residue((x + 1)/sqrt(x - 1), x, 1));
['residue,(x + 1)/sqrt(x - 1), x, 1]$

residue(sqrt(x)/(x^2 - 1), x, 1);
1/2$

residue(1/(x^(1/3)*(x - 1)), x, 1);
1$

nounform_return(residue(1/((x - 2)^(3/2)), x, 2));
['residue,1/(x-2)^(3/2),x,2]$

residue(sqrt(x + 2)/(x - 2), x, 2);
2$

residue(x^(1/2)/(x - 1)^2, x, 1);
1/2$

residue(x^(1/3)/(x - a), x, a);
a^(1/3)$

nounform_return(residue((x - a)^(1/2)/(x - a), x, a));
['residue, (x - a)^(1/2)/(x - a), x, a]$

residue(x^(3/2)/(x^2 - 1), x, 1);
1/2$

nounform_return(residue(1/(x - 1)^(3/2), x, 1));
['residue,1/(x-1)^(3/2),x,1]$

nounform_return(residue((x^2 + 1)/x^(1/2), x, 0));
['residue, (x^2 + 1)/x^(1/2), x, 0]$

/* x -> 1/ (sqrt(x - a)*sqrt(x + a)) has a branch point at a, so no residue at a*/
nounform_return(residue(1/(sqrt(x - a)*sqrt(x + a)), x, a)); 
['residue, 1/(sqrt(x - a)*sqrt(x + a)), x, a]$

/* x -> 1/ sqrt(x^2-1) has a branch point at 1, so no residue at 1*/
nounform_return(residue(1/(sqrt(x^2 - 1)), x, 1)); 
['residue, 1/(sqrt(x^2 - 1)), x, 1]$

nounform_return(residue(1/(x^(2/3) + 1), x, 0));
['residue, 1/(x^(2/3) + 1), x, 0]$ 

residue((x + 1)^(1/2)/(x - 2), x, 2);
sqrt(3)$

residue(x^(1/2)/(x - %i), x, %i);
(-1)^(1/4)$

nounform_return(residue((x - b)/(x - a)^(3/2), x, a));
['residue, (x - b)/(x - a)^(3/2), x, a]$

/* trigonometric functions*/
residue(sin(a*x),x,0);
0$

residue(1/sin(a*x),x,0);
1/a$

residue(1/sin(x),x,%pi);
-1$

block([ans],
  declare(n,integer),
  ans : residue(1/sin(x),x,n*%pi),
  remove(n,integer),
  ans);
(-1)^n$

residue(sin(x)^2,x,0);
0$

residue(1/sin(a*x),x,0);
1/a$

residue(1/sin(x)^2,x,0);
0$

residue(1/sin(x)^5,x,0);
3/8$

residue(%pi * cot(%pi * x)/(x^2 + a^2),x,%i*a);
-(%pi/(2*a*tanh(%pi*a)))$

residue(1/sin(x),x,0);
1$

residue(1/sin(x),x,%pi);
-1$

residue(1/sin(x)^2,x,0);
0$

residue(1/sin(x)^3,x,0);
1/2$

/* branch points */
nounform_return(residue(sqrt(x),x,0));
['residue,sqrt(x),x,0]$

/* log functions */
nounform_return(residue(log(x)/x, x, 0));
['residue,log(x)/x, x, 0]$

residue(log(x)/(x - 1), x, 1);
0$

residue(log(x)/(x - a), x, a);
log(a)$

nounform_return(residue(x*log(x), x, 0));
['residue, x*log(x),x,0]$

residue(log(x)/(x^2 + 1), x, %i);
log(%i)/(2*%i)$

residue(log(x)/(x^2 + 1), x, -%i);
 (%i*log(-%i))/2$

residue(log(x^2 + 1)/(x - 1), x, 1);
log(2)$

nounform_return(residue(log(x + 2)/(x + 2), x, -2));
['residue, log(x + 2)/(x + 2), x, -2]$

residue(log(x)/(x - 2)^2, x, 2);
1/2$

block([logexpand : false], residue(log(x^2)/(x + 1), x, -1));
0$

residue(log(x + a)/(x - b), x, b);
log(b + a)$

residue(x^2*log(x)/(x - 1), x, 1);
0$

residue(log(x)/(x^2 - 1), x, 1);
0$

residue(log(x)/(x^2 - 1), x, -1);
log(-1)/(-2)$

residue(log(x)/(x - %i), x, %i);
log(%i)$

residue(log(1/x)/(x - 1), x, 1);
0$

residue(log(x^2 + 4)/(x - 2), x, 2);
log(8)$

nounform_return(residue(log(x - 1)/(x - 1)^2, x, 1));
['residue, log(x - 1)/(x - 1)^2, x, 1]$

residue(log(a*x)/(x - a), x, a);
log(a^2)$

residue(log(x + 1)/(x - a), x, a);
log(a + 1)$

/* products of gamma functions*/
residue(gamma(x),x,0);
1$

residue(gamma(x)*gamma(x-1),x,0);
2*%gamma - 1$


residue(gamma(x)*gamma(x-1)*gamma(x-2),x,0);
-((%pi^2+18*%gamma^2-30*%gamma+17)/8)$

residue(gamma(x),x,0);
1$

residue(gamma(x),x,-1);
-1$

residue(gamma(x),x,-3);
-1/6$

residue(1/gamma(x),x,0);
0$

residue(1/gamma(x)^2,x,0);
0$

residue(gamma(x + 1)/(x + 1),x,-1);
-%gamma$

residue(gamma(x)/(x + 2),x,-2);
-((2*%gamma-3)/4)$

residue(gamma(x)*x^2,x,-2);
2$

residue(gamma(x)*sin(%pi*x),x,-1);
0$

residue(gamma(x)/(x + a),x,-a);
gamma(-a)$

/* Simple pole at x = 0 */
residue(1/x, x, 0);                  
1$

/* Second-order pole at x = 0 */
residue(1/x^2, x, 0);                
0$

/* Simple pole with polynomial numerator */
residue((x + 1)/(x - 2), x, 2);      
3$

/* Pole at x = -1 */
residue(1/(x + 1), x, -1);          
1$

residue(1/sin(x), x, 0);      
1$

residue(1/cos(x), x, %pi/2);            
-1$

residue(1/tan(x), x, %pi/2);       
0$

residue(1/sec(x), x, %pi/2);               
0$

residue(1/csc(x), x, %pi);           
0$

residue(sin(x)/(x - %pi), x, %pi);          
0$

residue(cos(x)/(x - %pi), x, %pi);          
-1$

residue(tan(x)/(x - %pi), x, %pi);          
0$

residue(sec(x)/(x - %pi), x, %pi);          
-1$

residue(csc(x)/(x - %pi), x, %pi);          
0$

residue(1/asin(x), x, 0);      
1$

residue(1/atan(x), x, 0);                
1$

residue(1/acos(x), x, 0);                 
0$

residue(1/(x*sin(x)), x, 0);               
0$

residue(1/(x*cos(x)), x, 0);                
1$

residue(1/(x^2*sin(x)), x, 0);              
1/6$

residue(sin(x)/(x - %i), x, %i);            
sin(%i)$

residue(cos(x)/(x + %i), x, -%i);           
cos(-%i)$

residue(tan(x)/(x - %pi + %i), x, %pi - %i);
-%i*tanh(1)$

/* Bessel functions*/
residue(bessel_j(0, x)/x, x, 0);
1$

residue(bessel_j(1, x)/x^2, x, 0);
1/2$

residue(1/bessel_j(0, x), x, %pi);
0$   

nounform_return(residue(bessel_y(0, x), x, 0));
['residue,bessel_y(0, x), x, 0]$  /* Logarithmic singularity at x = 0 */

residue(bessel_y(1, x), x, 0);
-2/%pi$

residue(bessel_j(0, x)/(x - 1), x, 1);
bessel_j(0, 1)$

residue(bessel_j(1, x)/(x - a), x, a);
bessel_j(1, a)$

residue(bessel_y(0, x)/(x - a), x, a);
bessel_y(0, a)$

residue(bessel_j(0, x)/(x - %i), x, %i);
bessel_j(0, %i)$

residue(bessel_y(1, x)/x, x, 0);
0$

/* Residue Tests: Log times Trig Function */
residue(log(x)*sin(x)/(x - %pi), x, %pi);
0$

residue(log(x)*cos(x)/(x - 1), x, 1);
0$

residue(log(x)*tan(x)/(x - 1/2), x, 1/2);
-(tan(1/2)*log(2))$

/* we assume that a # 0 and cos(a) # 0*/
residue(log(x)*sec(x)/(x - a), x, a);
 log(a)/cos(a)$

/* I think this should give a nounform return--the residue doesn't exist*/
errcatch(residue(log(x^2 + 1)*sin(x)/(x - %i), x, %i)); 
['residue(log(x^2 + 1)*sin(x)/(x - %i), x, %i)]$

residue(log(x + 1)*cos(x)/(x - 2), x, 2);
cos(2)*log(3)$

residue(log(x - 1)*csc(x)/(x - %pi/2), x, %pi/2);
 log((%pi-2)/2)$

residue(log(x)*cot(x)/(x - %pi/4), x, %pi/4);
log(%pi/4)$

residue(log(x^2 + 4)*sec(x)/(x), x, 0);
log(4)$

residue(log(x)*sin(%pi*x)/(x - 1), x, 1);
0$

 /* polylog tests */
 
 /* Since x -> li[2](x)/(x-1) has a branch point at 1, the residue isn't defined.*/
nounform_return(residue(li[2](x)/(x - 1), x, 1));
['residue, li[2](x)/(x - 1), x, 1]$

block([ans],
  assume(notequal(a,1)),
  ans : residue(li[3](x)/(x - a), x, a),
  forget(notequal(a,1)),
  ans);
li[3](a)$

residue(li[1](x)/(x - 1), x, 1);
residue(-log(1-x)/(x-1),x,1)$

residue(li[2](x)/(x - %i), x, %i);
(48*%catalan*%i-%pi^2)/48$

residue(li[2](8/x), x, 0);
8$   

/* polygamma functions */
residue(psi[0](x)/(x - 1), x, 1);
-%gamma$

residue(psi[0](x)/(x + 1), x, -1);
1-%gamma$   /* pole at x = -1 */

residue(psi[0](x)/(x^2 + 1), x, %i);
psi[0](%i)/(2*%i)$

/* are you sure about this one? */
residue(x*psi[0](x), x, 0);
0$   

residue(psi[1](x)/(x - 2), x, 2);
(%pi^2 - 6)/6$

residue(psi[0](x)/(x - a), x, a);
psi[0](a)$

residue(psi[0](x^2)/(x - 1), x, 1);
-%gamma$

residue(psi[0](x + 1)/(x + 1), x, -1);
-%gamma$  

residue(psi[0](x)/(x - %i), x, %i);
psi[0](%i)$

residue(log(x)*psi[0](x)/(x - 1), x, 1);
0$

residue(psi[1](x)/(x - 1), x, 1);
%pi^2/6$

residue(psi[2](x)/(x - a), x, a);
psi[2](a)$

residue(psi[3](x)/(x - 2), x, 2);
(%pi^4 - 90)/15$

residue(psi[1](x^2)/(x - 1), x, 1);
%pi^2/6$   

residue(psi[4](x)/ (x + 3), x, -3);
-((7776*zeta(5)-8051)/324)$

residue(psi[1](x + a)/(x - a), x, a);
psi[1](2*a)$

residue(x*psi[3](x), x, 0);
0$   

residue(log(x)*psi[2](x)/(x - 1), x, 1);
0$  

/* tricky cases */
residue((x^2 - 1)/(x - 1), x, 1);
0$  

residue(exp(1/x), x, 0);
1$  

residue(x/(x + 1)^2, x, -1);
1$   

residue(log(x)/(x - a)^2, x, a);
1/a$ 

residue(tan(x), x, %pi/2);
-1$ 

residue(gamma(x)^2, x, -1);
2 - 2*%gamma$   

residue((x - a)/((x - a)^2 + b^2), x, a + %i*b);
1/2$  

residue((sin(x) - x)/(x^3), x, 0);
0$   

residue(x^2/(x^2 + 1)^2, x, %i);
-%i/4$   

residue(x^2/(x^3 + x), x, 0);
0$   

residue(sin(x)/(x*(x-%pi)),x,0);
0$

residue(sin(x)/(x*(x-%pi)),x, '%pi);
0$

residue(sin(x)/(x*(x-1)),x,1);
sin(1)$

block([ans : map(lambda([pt], residue(z^2 * log(z)/(1+z^4),z,pt)), map(rhs, solve(1+z^4,z)))],
 [ans, rectform(xreduce("+",ans))]);
 [[((-1)^(3/4)*%i*log((-1)^(1/4)*%i))/4,((-1)^(3/4)*log(-(-1)^(1/4)))/4,
        -(((-1)^(3/4)*%i*log(-((-1)^(1/4)*%i)))/4),
        -(((-1)^(3/4)*log(-1))/16)],%pi/2^(3/2)]$

map(lambda([pt], residue(1/((x - 8)*(x - 7)*(x - 9))^4, x,pt)), [7,8,9]);
[105/32,0,-(105/32)]$

residue((a + x)*(1 - a - x)/x, x, 0);
a - a^2$

/* symbolic order */

(declare(n,integer), assume(n >0),0);
0$

/* could do an askequal on n, I suppose*/
nounform_return(residue(exp(z)/(z - a)^n, z, a));
['residue, exp(z)/(z - a)^n, z, a]$

(remove(n,integer), forget(n > 0), 0);
0$

/* miscellaneous */
residue(1/(exp(x) - 1), x, 0);
1$

residue(1/(exp(x) - 1 - x), x, 0);
 -2/3$

/* could do, I suppose, an askequal on a*/
residue(1/(exp(x) - 1 - a*x), x, 0);
-(1/(a-1))$

/* could do an askequal(n,1), I suppose, but this is not wrong*/
nounform_return(residue(1/(x-1)^n,x,1));
['residue, 1/(x-1)^n,x,1]$

nounform_return(residue(exp(x)/(x-1)^n,x,1));
['residue, exp(x)/(x-1)^n,x,1]$

(remove(n,integer),0);
0$

/* \#4014 defint() produces incorrect output*/
block([ans], assume(sin(phi) > 0), 
             ans : defint(1/(w^4 - 2*cos(2*phi) * w^2 + 1),w,minf,inf), 
             forget(sin(phi) > 0),
             ans);
%pi/(2*sin(phi))$

/* \#4579 residue fails in integration (setting algebraic : true gives nicer answer)*/
block([algebraic : true], integrate(sin(x)^10/(8+cos(x)), x, -%pi, %pi));
-(((314928*7^(9/2)-2000564027)*%pi)/8)$

/* maybe bug 4579 was supposed to be: */
integrate(sin(x)^10/(5+3*cos(x)), x, -%pi, %pi);
(384143*%pi)/3779136$

/* \#4580 residue with constant symbolic constants */
residue(f(a)/(x-1),x,1);
f(a)$

/* \#4582 residue(sin(1/(z-1)), z, 1);*/
residue(sin(1/(z-1)), z, 1);
1$

/* \#4590 integrals of some rational functions from minf to inf */
integrate(1/(x^2 + %i+a),x,minf,inf);
%pi/sqrt(a+%i)$

/*\#4598 a definite integral with demoivre : false 
block([demoivre : true], integrate(x^8/(5+x^9)^2,x,0,inf));
1/45$*/

block([demoivre : false], integrate(x^8/(5+x^9)^2,x,0,inf));
1/45$

/* \# 4595 residue bug*/
block([ans],
ans : residue(plog(x)/(x-%i*sin(%pi/9)-cos(%pi/9)),x,%e^((%i*%pi)/9)),
[ans, subst('plog ='log,ans)]);
[plog(%e^((%i*%pi)/9)),(%i*%pi)/9]$

/*  \#4597 residue should check if taylor series is a power series*/
nounform_return(residue(log(1-x)/(1-x),x,1));
['residue, log(1-x)/(1-x),x,1]$

block([ans],
 assume(notequal(a,0)),
 ans : residue(a/x,x,a),
 forget(notequal(a,0)),
 ans);
 0$

block([ans],
 assume(equal(a,0)),
 ans : residue(a/x,x,a),
 forget(equal(a,0)),
 ans);
 0$

block([ans],
assume(a>0),
ans : [residue(exp(a/x),x,0), residue(x*exp(a/x),x,0), residue(exp(a/x)/x,x,0)],
forget(a>0),
ans);
[a,a^2/2,1]$

residue(exp(-1/x),x,0);
-1$

/* zero is a branch point of x -> x^(5/2) * exp(1/sqrt(x)), so no residue at zero. */
nounform_return(residue(x^(5/2) * exp(1/sqrt(x)),x,0));
[residue,%e^(1/sqrt(x))*x^(5/2),x,0]$

/* see https://math.stackexchange.com/questions/4700802/residues-of-dual-pole-%CE%93-functions*/

residue(gamma(-s+a-1)*gamma(a-s)*gamma(b-s)*gamma(c-s)*z^s,s,a);
(gamma(b-a)*psi[0](c-a)*gamma(c-a)+(psi[0](b-a)*gamma(b-a)+(1-2*%gamma)*gamma(b-a))*gamma(c-a))
 *z^a -gamma(b-a)*gamma(c-a)*z^a*log(z)$

residue(gamma(-s+a-1)*gamma(a-s)*gamma(b-s)*gamma(c-s)*z^s,s,a+3);
-((12*gamma(b-a-3)*gamma(c-a-3)*z^(a+3)*log(z)
 +(((24*%gamma-47)*gamma(b-a-3)-12*psi[0](b-a-3)*gamma(b-a-3))*gamma(c-a-3)
  -12*gamma(b-a-3)*psi[0](c-a-3)*gamma(c-a-3))
  *z^(a+3))
 /1728)$

/* see https://github.com/sympy/sympy/issues/21176 */
residue(x**2*cot(%pi*x)/(x**4 + 1), x, -sqrt(2)/2 - sqrt(2)*%i/2);
-((%i-1)/(2^(5/2)*tan(((sqrt(2)*%i+sqrt(2))*%pi)/2)))$

/* residue(exp(1/x)/(x+a),x,0); */

block([ans],
 assume(n >= 2),
 declare(n,even),
 declare(n,integer),
 ans : residue(sin(a*x)/x^n,x,0),
 forget(n >= 2),
 remove(n,even),
 remove(n,integer),
 ans);
 ((-1)^((n-2)/2)*a^(n-1))/(n-1)!$

 block([ans],
 assume(n >= 0),
 declare(n,even),
 ans : residue(tan(a*x)/x^n,x,0),
 forget(n >= 0),
 remove(n,even),
 ans);
((-1)^(n/2-1)*(2^n-1)*2^n*a^(n-1)*bern(n))/n!$

/* is this correct? */
residue(bessel_j(k,x)/x^4,x,0);
-((bessel_j(k+3,0)-3*bessel_j(k+1,0)+3*bessel_j(k-1,0)-bessel_j(k-3,0))/48)$


/* The following tests are adapted from Guo Yicong’s [Maxima residue package](https://github.com/guoyicong/Maxima_residue/tree/master). 
This work is released under GNU General Public License v3.0.  I thank Guo Yicong for making these tests available. */
residue(1/(z^7+1), z,exp(%i*%pi/7));
-(%e^((%i*%pi)/7)/7)$

residue(sin(1/z),z,0);
1$

residue(1/sin(z), z, 2*%pi);
1$

residue(1/(z-1), z, 1);
1$

residue(cos(z)/(z-1)^2/(z+4), z, -4);
cos(4)/25$

residue(z*cos(1/z), z, 0);
-1/2$

nounform_return(residue(cos(z)*sin(1/z), z, 0));
['residue, cos(z)*sin(1/z), z, 0]$

residue(z^2*sin(1/z), z, 0);
-1/6$

residue(exp(1/z)/(1-z), z, 0);
-1$

nounform_return(residue(exp(z)*exp(1/z), z, 0));
['residue, exp(z)*exp(1/z), z, 0]$

residue(exp(1/z^2)/(1-z), z, 0);
-1$

residue(z^2*sin(1/(z-1)), z, 1);
5/6$

/* end of Guo Yicong’s tests */

/* Tests collected from the mailing list */

/* https://sourceforge.net/p/maxima/mailman/message/22347358/ */

/* The nounform return is suboptimal, but it isn't wrong.*/
block([domain : complex, ans],
 declare(N,integer),
 assume(N > 0, y > 0),
 ans : nounform_return(residue(1 / (x*(x-x0)*(1 - x^N/y)),x,0)),
 remove(N,integer),
 forget(N > 0, y > 0),
 ans);
['residue, 1 / (x*(x-x0)*(1 - x^N/y)),x,0]$

block([domain : complex, ans],
 declare(N,integer),
 assume(N > 0, y > 0),
 ans : residue(1 / (x*(x-x0)*(1 - x^N/y)),x,x0),
 remove(N,integer),
 forget(N > 0, y > 0),
 ans);
 y/(x0*y-x0^(N+1))$

 block([ans],
   declare([p,N],integer),
   assume(N > 0, y > 0),
   ans : nounform_return(residue(x^(p-1) / ((x - x0)*(1 - x^N/y)),x,0)),
   remove([p,N], integer),
   forget(N > 0, y > 0),
   ans);
['residue, x^(p-1)/((x-x0)*(1-x^N/y)),x,0]$

block([ans],
   declare([p,N],integer),
   assume(N > 0, y > 0),
   ans : nounform_return(residue(x^(-p-1) / ((x - x0)*(1 - x^N/y)),x,0)),
   remove([p,N],integer),
   forget(N > 0, y > 0),
   ans);
['residue,x^(-p-1)/((x-x0)*(1-x^N/y)),x,0]$

block([ans],
	   declare([k,N],integer),
     assume(N > 0, y > 0),
	   ans : nounform_return(residue(x^(k-1) / ((x - x0)*(1 - x^N/y)),x,0)),
	   remove([k,N],integer),
     forget(N > 0, y > 0),
	   ans);
['residue, x^(k-1) / ((x - x0)*(1 - x^N/y)),x,0]$

/* https://sourceforge.net/p/maxima/mailman/maxima-discuss/thread/87czewybvj.fsf%40t-online.de/#msg37672791*/

residue (5/(s - a), s, a);
5$

block([ans],
  declare(a, complex),
  ans : residue(5/(s - a), s, a),
  remove(a,complex),
  ans);
5$

residue(5/(w - exp(%i)), w, exp(%i));
5$

residue(5/(w - exp(%i*phi)), w, exp(%i*phi));
5$

/* https://sourceforge.net/p/maxima/mailman/message/29524668/ */
residue(1/(x^8+1),x,exp(%i*%pi/8));
(%e^((7*%i*%pi)/8)*%i+%e^((7*%i*%pi)/8))/2^(7/2)$

/* https://sourceforge.net/p/maxima/mailman/message/22682703/ */

residue(exp(-(m1-m2)*T/RC) * 1/(x - x0) * x^(N*m2) * x/(x - 1), x, 1);
-(%e^((T*(m2-m1))/RC)/(x0-1))$

block([m1 : 1-a, m2 : 1-b], residue(exp(-(m1-m2)*T/RC) * 1/(x - x0) * x^(N*m2) * x/(x - 1), x, 1));
-(%e^((T*(a-b))/RC)/(x0-1))$

/* https://ask.sagemath.org/question/9141/calculating-residue-with-maxima_methods/ */
residue(( x/((4*x - 1)*(t^2 - t + x)) ),t,(1-sqrt(1-4*x))/2);
(sqrt(1-4*x)*x)/(16*x^2-8*x+1)$

/* https://sourceforge.net/p/maxima/mailman/message/37243354/ */

block([xxx : makelist(makelist(residue(1/(z^n+1),z, exp((2*k+1)*%i*%pi/n)),k,0,n-1),n,0,7),
       yyy : makelist(makelist( 1/(n * exp(%i * %pi * (2*k+1) * (n-1)/n)),k,0,n-1),n,0,7)],
      trigrat(xxx-yyy));
[[],[0],[0,0],[0,0,0],[0,0,0,0],[0,0,0,0,0],[0,0,0,0,0,0],[0,0,0,0,0,0,0]]$

/* See https://sourceforge.net/p/maxima/mailman/message/21331154/ */
integrate((-14*x^2-32)/(x^4+3*x^2+1)^2,x,0,inf);
-((103*%pi)/(2*5^(3/2)))$

/* https://sourceforge.net/p/maxima/mailman/message/37742912/  Not sure if this is a residue problem
integrate(x^2/((x^2-a^2)^2+g^2*x^2),x,-inf,inf); */

/* test residue-by-simp */
residue(a*f(x)+ g(x)/%pi + c/(x-a),x,a);
residue(g(x),x,a)/%pi+a*residue(f(x),x,a)+c$

/* does residue respect quotes? */
nounform_return('residue(exp(1/x),x,0));
['residue, exp(1/x),x,0]$

values;
[]$

facts();
[]$

contexts;
['initial, 'global]$